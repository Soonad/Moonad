Research.Var : Type
  Pair(Char)(Nat)

Research.Var.new : Char -> Nat -> Research.Var
  Pair.new<Char><Nat>

Research.Ctx : Type
  List(Research.Var)

Research.Ctx.nil : Research.Ctx
  List.nil<Research.Var>

Research.Ctx.ext : Char -> Nat -> Research.Ctx -> Research.Ctx
  (nam) (x) List.cons<Research.Var>(Research.Var.new(nam)(x))

Research.Ctx.get : Char -> Research.Ctx -> Maybe(Nat)
  (chr) (ctx)
  ctx<() Maybe(Nat)>
  | Maybe.none<Nat>;
  | (h) (t)
    h<() Maybe(Nat)>
    | (nam) (var)
      U16.eql(nam)(chr)<() Maybe(Nat)>
      | Maybe.some<Nat>(var);
      | Research.Ctx.get(chr)(t);;;

Research.parse_fun.filt : String -> String
  String.filter((c) Bool.or
    | U16.eql(c)(Char.parse("位"));
    | U16.eql(c)(Char.parse("."));)

Research.parse_fun.typ0 : String -> Research.Ctx -> Type
  (str) (ctx)
  str<() Type>
  | Unit;
  | (nam) (nxt) Research.parse_fun.typ1(nam)(nxt)(ctx);

Research.parse_fun.typ1 : Char -> String -> Research.Ctx -> Type
  (nam) (nxt) (ctx)
  nxt<() Type>
  | Nat;
  | (h) (t) (x: Nat) -> Research.parse_fun.typ0(String.cons(h)(t))(Research.Ctx.ext(nam)(x)(ctx));

Research.parse_fun.term : (str:String) -> (ctx:Research.Ctx) -> Research.parse_fun.typ0(str)(ctx)
  (str) (ctx)
  str<(str) Research.parse_fun.typ0(str)(ctx)>
  | Unit.new;
  | (nam) (nxt)
    nxt<(str) Research.parse_fun.typ1(nam)(str)(ctx)>
    | Research.Ctx.get(nam)(ctx)<() Nat>
      | Nat.zero;
      | (n) n;;
    | (h) (t) (x) Research.parse_fun.term(String.cons(h)(t))(Research.Ctx.ext(nam)(x)(ctx));;

Research.fun : (str:String) -> Research.parse_fun.typ0(Research.parse_fun.filt(str))(Research.Ctx.nil)
  (str) Research.parse_fun.term(Research.parse_fun.filt(str))(Research.Ctx.nil)

Research.crazy: Nat -> Nat -> Nat -> Nat
  Research.fun("位x.位y.位z.y")

Research.it_works: Equal(Nat)(Nat.1)(Research.crazy(Nat.0)(Nat.1)(Nat.2))
  Equal.to<Nat><Nat.1>
  
  
