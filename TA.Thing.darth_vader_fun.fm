TA.Thing.darth_vader_fun : TA.Game.TxFunction
  (self) 
  let self     = TA.Thing.set_mov(self)(F64.parse("2"))
  let self     = TA.Thing.set_mhp(self)(F64.parse("24"))
  let self.act = TA.Thing.get_act(self)
  let self.dir = TA.Thing.get_dir(self)
  let self.pos = TA.Thing.get_pos(self)
  let self = 
    TA.Thing.update_buff
    | self;
    | TA.Constants.DARTH_VADER_USING_FORCE;

  Bool.if<TA.Thing>(F64.eql(self.act)(F64.0))
  | #DARTH_VADER_IDLE# 
    F64.if<>(TA.Thing.is_walking(self))
    | let is_using_buff = 
        TA.Thing.is_using_buff(self)(TA.Constants.DARTH_VADER_USING_FORCE)
      Bool.if<>(is_using_buff)
      | TA.Thing.animate(self)
        | F64.parse("1.6");
        | DARTH_VADER_WALK_CYCLE_000;
        | F64.parse("8");
        | F64.parse("16");
      ;
      | TA.Thing.animate(self)
        | F64.1;
        | DARTH_VADER_WALK_CYCLE_000;
        | F64.parse("8");
        | F64.parse("16");
      ; 
    ;

    | TA.Thing.animate(self)(F64.1)(DARTH_VADER_IDLE_000)
      | F64.parse("5");
      | F64.parse("10");
    ;
  ;

  | Bool.if<TA.Thing>(F64.eql(self.act)(F64.1))
  | #DARTH_VADER_THE_FORCE_AWAKENS# 
    let value = 
      TA.Thing.get_stt_value
      | self;
      | TA.Constants.DARTH_VADER_USING_FORCE;
    
    let inc = ((x) F64.add(F64.1)(x)) :: F64 -> F64
    let tik = TA.Thing.get_tik(self)
    let self = 
      Bool.if<>(F64.eql(tik)(F64.0))
      | TA.Thing.map_stt_key
        | self;
        | TA.Constants.FORCE_AWAKENS_KEY;
        | inc;;
      | self;

    let self = 
      Bool.if<>(F64.eql(F64.mod(value)(F64.parse("2")))(F64.0))
      | TA.Thing.animate(self)(F64.0)
        | DARTH_VADER_THE_FORCE_AWAKENS_VAR_1_000;
        | F64.parse("8");
        | F64.parse("16");
      ;
      | Bool.if<>(F64.eql(F64.mod(value)(F64.parse("3")))(F64.0))
        | TA.Thing.animate(self)(F64.0)
          | DARTH_VADER_THE_FORCE_AWAKENS_VAR_2_000;
          | F64.parse("8");
          | F64.parse("16");
        ;
        | TA.Thing.animate(self)(F64.0)
          | DARTH_VADER_THE_FORCE_AWAKENS_VAR_3_000;
          | F64.parse("8");
          | F64.parse("24");
        ;
      ;

    let self = 
      TA.Thing.dash(self)
      | F64.parse("2");
      | F64.0;
      | F64.parse("20");

    self;

  // TODO: Still need to add the force on his position 
  | Bool.if<TA.Thing>(F64.eql(self.act)(F64.2))
  | #DARTH_VADER_THE_EMPIRE_STRICKES_BACK# 
    let set_timer = 
      (() F64.mul(F64.parse("5"))(TA.Constants.ONE_SEC)) :: F64 -> F64

    let self = 
      TA.Thing.map_stt_key
      | self;
      | TA.Constants.DARTH_VADER_USING_FORCE;
      | set_timer;

    let self = 
      TA.Thing.animate
      | self;
      | F64.1;
      | DARTH_VADER_IDLE_000;
      | F64.parse("5");
      | F64.parse("10");
    self;

  | Bool.if<TA.Thing>(F64.eql(self.act)(F64.parse("3")))
  | #DARTH_VADER_THE_RISE_OF_SKYWALKER# 
    let self = 
      TA.Thing.animate_between
      | self;
      | F64.0;
      | DARTH_VADER_THE_RISE_OF_SKYWALKER_CAST_ANIMATION_000;
      | F64.parse("3");
      | F64.0;
      | F64.parse("12");

    let self = 
      TA.Thing.animate_between
      | self;
      | F64.0;
      | DARTH_VADER_THE_RISE_OF_SKYWALKER_CAST_ANIMATION_002;
      | F64.1;
      | F64.parse("12");
      | F64.parse("18");

    let self = TA.Thing.reset(self)(F64.parse("17"))
    let force = TA.Thing.new_thing
    let force = TA.Thing.set_fun(force)(TA.Thing.darth_vader.rise_skywalker_fun)
    let self.sid = TA.Thing.get_sid(self)
    let force = TA.Thing.set_sid(force)(self.sid)
    let self = 
      TA.Thing.spawn
      | self;
      | F64.parse("10");
      | [ TA.Thing; 
          TA.Thing.move
          | force;
          | TA.Thing.at_max_dist(self)(F64.parse("150"));
        ];
    self;

  | Bool.if<TA.Thing>(F64.eql(self.act)(F64.parse("4")))
  | #DARTH_VADER_THE_PHANTOM_MENACE# 
    let self = 
      TA.Thing.animate_between
      | self;
      | F64.0;
      | DARTH_VADER_THE_PHANTOM_MENACE_CAST_ANIMATION_000;
      | F64.parse("4");
      | F64.0;
      | F64.parse("12");

    let self = 
      TA.Thing.animate_between
      | self;
      | F64.0;
      | DARTH_VADER_THE_PHANTOM_MENACE_CAST_ANIMATION_004;
      | F64.1;
      | F64.parse("12");
      | F64.parse("18");

    let self = TA.Thing.reset(self)(F64.parse("17"))
    let self.sid = TA.Thing.get_sid(self)
    let targ_dir = TA.Thing.targ_dir(self)
    let saber = TA.Thing.new_thing
    let saber = TA.Thing.set_sid(saber)(self.sid)
    let saber = 
      TA.Thing.set_fun
      | saber;
      | TA.Thing.darth_vader.darth_vader_phanton_menace_twisting_fun;
    let saber = TA.Thing.set_pos(saber)(self.pos)
    let saber = TA.Thing.set_dir(saber)(targ_dir)
    let saber = TA.Thing.set_vel(saber)(F64.V3.scale(F64.parse("10"))(targ_dir))
    let saber = TA.Thing.set_box(saber)(TA.Game.Hitbox.nbox)
    let self = TA.Thing.spawn(self)(F64.parse("14"))([ TA.Thing; saber ])
    self;
     
  | Bool.if<TA.Thing>(F64.eql(self.act)(F64.parse("5")))
  | #DARTH_VADER_ROGUE_ONE# 
    let self = 
      TA.Thing.animate_with_blink
      | self;
      | F64.0;
      | DARTH_VADER_ROGUE_ONE_000;
      | DARTH_VADER_IDLE_000;
      | F64.parse("10");
      | F64.0;
      | F64.parse("20");

    let self = TA.Thing.reset(self)(F64.parse("20"))
    let self = 
      TA.Thing.blink
      | self;
      | F64.parse("20");
      | TA.Thing.at_dist(self)(F64.parse("75"));

    self ;

  | #DARTH_VADER_EXTRA# 
    TA.Thing.animate
    | self;
    | F64.1;
    | DARTH_VADER_IDLE_000;
    | F64.parse("5"); 
    | F64.parse("10"); 
  ;;;;;;