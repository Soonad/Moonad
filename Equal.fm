// The Equal datatype.
T Equal <A: Type> (a: A) ~ (b: A)
| to                     ~ (a);

Equal.apply_ind<A: Type, B: Type>(a: A, b: A, f: A -> B, e: Equal(A,a,b)): Equal(B, f(a), f(b))
  case e:
  | Equal.to<B><f(a)>;
  : Equal(B, f(a), f(e.b));

Equal.chain<A: Type, a: A, b: A, c: A>(d: Equal(A,a,b), e: Equal(A,b,c)): Equal(A,a,c)
  case e:
  | d;
  : Equal(A, a, e.b);

Equal.mirror<A: Type, a: A, b: A>(e: Equal(A,a,b)): Equal(A,b,a)
  case e:
  | Equal.to<A, a>;
  : Equal(A, e.b, a);

Equal.rewrite<A: Type, a: A, b: A, P: A -> Type>(e: Equal(A,a,b), x: P(a)): P(b)
  case e:
  | x;
  : P(e.b);

Equal.apply<A: Type, B: A -> Type, a: A, b: A>(f: (x: A) -> B(x), e: Equal(A, a, b))
  : Equal(B(a), f(a), Equal.rewrite<A,b,a,B>(Equal.mirror<A,a,b>(e))(f(b)))
  case e:
  | Equal.to<B(a)><f(a)>;
  : Equal(B(a), f(a), Equal.rewrite<A,e.b,a,B>(Equal.mirror<A,a,e.b>(e.self),f(e.b)));
